plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        expand(project.properties)
    }
}

repositories {
    maven {
        url 'http://ptms.ink:8081/repository/releases/'
        allowInsecureProtocol true
    }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

dependencies {
    compileOnly 'ink.ptms.core:v11802:11802:universal'
    compileOnly 'ink.ptms.core:v11701:11701:universal'
    compileOnly 'me.clip:placeholderapi:2.10.9'

    compileOnly 'org.jetbrains:annotations:21.0.0'

    // Shaded Dependencies
    implementation 'com.github.Osiris-Team:Dream-Yaml:6.8'
    implementation 'com.saicone.rtag:rtag:1.1.0'
    implementation 'com.saicone.rtag:rtag-item:1.1.0'

    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
}

compileJava {
    options.release = 11
    options.encoding = 'UTF-8'
}

shadowJar {
    relocate 'com.osiris.dyml', project.group + '.lib.dyml'
    relocate 'com.saicone.rtag', project.group + '.lib.rtag'
    minimize()
}

jar.dependsOn (shadowJar)