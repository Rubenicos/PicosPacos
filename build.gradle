plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'me.rubenicos.mc.picospacos'
version = '0.1.2'

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        expand(
                'version': project.version
        )
    }
}

repositories {
    // MC
    maven { url 'https://repo2s.ptms.ink/repository/maven-releases/' }
    // PlaceholderAPI
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    // DreamYaml
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

dependencies {
    compileOnly 'ink.ptms.core:v11701:11701:mapped'
    compileOnly 'ink.ptms.core:v11701:11701:universal'
    compileOnly 'me.clip:placeholderapi:2.10.9'

    // Shaded Dependencies
    implementation 'com.github.Osiris-Team:Dream-Yaml:6.8'
    implementation 'org.jetbrains:annotations:21.0.0'

    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
}

compileJava {
    options.release = 11
    options.encoding = 'UTF-8'
}

shadowJar {
    relocate 'com.osiris.dyml', project.group + '.lib.dyml'
    relocate 'org.jetbrains', project.group + '.lib.annotations'
    minimize()
}

jar.dependsOn (shadowJar)